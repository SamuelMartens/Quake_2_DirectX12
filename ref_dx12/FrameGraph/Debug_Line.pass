PassProperties
{
PassInput : Debug
PassVertAttr: VertexIn
}

State
{
ColorTarget  : COLOR_BACK_BUFFER
DepthTarget  : DEPTH_BACK_BUFFER
Viewport	 : 0.0, 0.0, 1.0, 1.0
BlendEnabled : false
Topology	 : Line
}

RootSignature
{
RootFlags (ALLOW_INPUT_ASSEMBLER_INPUT_LAYOUT),
CBV(b0, space = 0),
CBV(b1, space = 0),
CBV(b0, space = 1),
CBV(b0, space = 2),
CBV(b0, space = 3)
}

// -== Register space convention ==-
//	0 - Global, PerPass
//	1 - Global, PerObject
//	2 - Local, PerPass
//	3 - Local, PerObject


// Attributes

struct VertexIn
{
	float4 Pos : POSITION;
}


struct VertexOut
{
	float4 Pos : SV_POSITION;
}

[Global, PerPass]
cbuffer cbFrameData : register(b0, space0)
{
	float4x4 InvertedViewProj;
	float4x4 InvertedProj;
    float4x4 ViewProj;
	float4x4 View;
    float4 CameraYaw;
    float4 CameraPitch;
    float4 CameraRoll;
    float4 CameraOrigin;
	float CameraNear;
	float CameraFar;
	int ScreenWidth;
	int ScreenHeight;
	int LightListSize;
}

[Global, PerPass]
cbuffer cbTileData : register(b1, space0)
{
	int TileWidth;
	int TileHeight;
	int NumFrustumSlices;
	int ClusterListSize;
}

[Global, PerObject]
cbuffer cbPerObject_Global : register(b0, space1)
{
	int DebugObjectType;
}

[Local, PerPass]
cbuffer cbPerPass_Local : register(b0, space2)
{
	float4x4 FrustumClusterInvertedView;
}

[Local, PerObject]
cbuffer cbPerObject_Local : register(b0, space3)
{
	float4 Radiance;
	int BounceNum;
	int FrustumClusterIndex;
	int IsActiveFrustumCluster;
	int IsLightAffectedFrustumCluster;
	int IsAnyLightAffectedFrustumCluster;
}

// Shaders

Type : VS 
External : cbFrameData, cbPerPass_Local, cbPerObject_Global, VertexIn, VertexOut
<[

#include "Debug.passh"

VertexOut main(VertexIn vIn)
{
	VertexOut vOut;
	
	vOut.Pos = vIn.Pos;
	
	if (DebugObjectType == DEBUG_OBJECT_FRUSTUM_CLUSTER)
	{
		vOut.Pos = mul(FrustumClusterInvertedView, vOut.Pos);
	}
	
	// Funny enought, by default matrices are packed as column major.
	vOut.Pos = mul(ViewProj, vOut.Pos);
	

	return vOut;
}
]>

Type : PS 
External : VertexOut, cbPerObject_Global, cbPerObject_Local, cbFrameData, cbTileData
<[

#include "Debug.passh"
#include "Constants.passh"
#include "ClusteredLighting.passh"

#define ENABLE_SHOW_ONLY_FRUSTUM_CLUSTER_WITH_LINEAR_INDEX 0
#define SHOW_ONLY_FRUSTUM_CLUSTER_WITH_LINEAR_INDEX 0

#define ENABLE_SHOW_ONLY_FRUSTUM_CLUSTER_WITH_3D_INDEX 0
#define SHOW_ONLY_FRUSTUM_CLUSTER_WITH_3D_INDEX uint3(0, 0, 0)

float4 main(VertexOut vOut) : SV_Target
{
	if (
	DebugObjectType != DEBUG_OBJECT_PATH_SEGMENT &&
	DebugObjectType != DEBUG_OBJECT_LIGHT_SAMPLE &&
	DebugObjectType != DEBUG_OBJECT_FRUSTUM_CLUSTER)
	{
		discard;
	}

	float4 color = float4(0.0, 0.0, 1.0, 1.0);

	if (DebugObjectType == DEBUG_OBJECT_LIGHT_SAMPLE)
	{
		color = float4(0.0, 1.0, 0.0, 1.0);
	}
	
	
	if (DebugObjectType == DEBUG_OBJECT_FRUSTUM_CLUSTER)
	{
	
#if ENABLE_SHOW_ONLY_FRUSTUM_CLUSTER_WITH_LINEAR_INDEX
		if (FrustumClusterIndex != SHOW_ONLY_FRUSTUM_CLUSTER_WITH_LINEAR_INDEX)
		{
			discard;
		}
#endif	

#if ENABLE_SHOW_ONLY_FRUSTUM_CLUSTER_WITH_3D_INDEX
		{
			const uint3 numClusters = GetNumClusters3D();
			const uint clusterLinearIndex = GetLinearClusterIndex(SHOW_ONLY_FRUSTUM_CLUSTER_WITH_3D_INDEX, numClusters);
			
			if (FrustumClusterIndex != int(clusterLinearIndex))
			{
				discard;
			}
		}
#endif 
	
		if (IsActiveFrustumCluster > 0)
		{
			// Orange
			color = float4(1.0f, 0.71f, 0.0f, 1.0f);
		}
		
		if (IsLightAffectedFrustumCluster > 0)
		{
			// Green
			color = float4(0.0f, 1.0f, 0.0f, 1.0f);
		}
		
		if (IsAnyLightAffectedFrustumCluster > 0)
		{
			// Red
			color = float4(1.0f, 0.0f, 0.0f, 1.0f);
		}
	}
	
    return color;
}
]>