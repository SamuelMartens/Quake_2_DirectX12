Pass <- PassProps State RSig PrePostPass ShaderDefs ShaderList

ShaderDefs	<- T( < ( Resource / VertAttr / Function )* > )
State       <- T( 'State' _ '{' _ < ( ColorTargetSt / DepthTargetSt / ViewportSt / BlendEnabledSt / TopologySt / DepthWriteMaskSt / SrcBlendSt / DestBlendSt )* > '}' )
ShaderList  <- T( < Shader* > )

# --- Pass properties
PassProps               <- T( 'PassProperties' _ '{' < ( PassInput / PassVertAttr / PassVertAttrSlots / PassThreadGroups )* > '}' )
PassInput               <- T( 'PassInput' _ ':' PassInputIdent )
PassInputIdent          <- _ 'Static' _ / _ 'Dynamic' _ / _ 'Particles' _ / _ 'UI' _ / _ 'PostProcess' _ / _ 'Debug' _
PassVertAttr            <- T( 'PassVertAttr' _ ':' Ident )
PassVertAttrSlots       <- T( 'PassSlotLayout' _ ':' VertAttrSlots )
PassThreadGroups        <- T( 'PassThreadGroups' _ ':' Int Comma Int Comma Int )

# --- Shader code ---
ShaderExternalDecl  <- T( 'External'_ ':' < Ident? ( ~Comma Ident )* > )
ShaderSource        <- T( '<[' < ( !"]>"  . )* > ']>' ) 
Shader              <- T( < ShaderTypeDecl ShaderExternalDecl ShaderSource > )
ShaderTypeDecl      <- T( 'Type' _ ':' _ < ShaderType > ) 
ShaderType			<- _'VS' _ / _ 'GS' _ / _ 'PS' _ / _ 'CS' _

# --- State ---
ColorTargetSt   	<- T( 'ColorTarget' _ ':' < Ident > )
DepthTargetSt   	<- T( 'DepthTarget' _ ':' < Ident > )
ViewportSt      	<- T( 'Viewport' _ ':' < (Float / Int) (~Comma (Float / Int)){3} > )
BlendEnabledSt  	<- T( 'BlendEnabled' _ ':' < Bool > )
SrcBlendSt			<- T( 'SrcBlend' _ ':' < BlendStValues > )
DestBlendSt			<- T( 'DestBlend' _ ':' < BlendStValues > )
TopologySt			<- T( 'Topology' _ ':' < TopologyStValues > )
DepthWriteMaskSt	<- T( 'DepthWriteMask' _ ':' < Bool > )

BlendStValues		<- _ 'SrcAlpha' _ / _ 'InvSrcAlpha' _
TopologyStValues    <- _ 'Triangle' _ / _ 'Point' _

# --- Root Signature ---
RSig                <- T( 'RootSignature' _ '{' < RSigFlagsDecl ( Comma RSigEntity )+ > '}' )
RSigEntity          <- RSigCBVDecl / RSigSRVDecl / RSigUAVDecl / RSigRootConstDecl / RSigStatSamplerDecl / RSigDescTableDecl

RSigFlagsDecl   	<- T( < 'RootFlags' _ '(' RSigFlags? ( _ '|' _ RSigFlags )* ')' > )
RSigFlags       	<- _ '0' _ / _ 'ALLOW_INPUT_ASSEMBLER_INPUT_LAYOUT' _ / _ 'DENY_VERTEX_SHADER_ROOT_ACCESS' _ / _ 'DENY_PIXEL_SHADER_ROOT_ACCESS' _

RSigCBVDecl         <- T( < 'CBV' _ '(' RegisterId ( Comma RSigDeclOptions )* ')' > )
RSigSRVDecl         <- T( < 'SRV' _ '(' RegisterId ( Comma RSigDeclOptions )* ')' > )
RSigUAVDecl         <- T( < 'UAV' _ '(' RegisterId ( Comma RSigDeclOptions )* ')' > )

RSigRootConstDecl	<- T( 'RootConstants' _ '(' _ 'num32BitConstants' _ '=' Int Comma RegisterId ( Comma RSigVisibilityDecl)? ')' )

RSigStatSamplerDecl <- T( 'StaticSampler' _ '(' RegisterId ( Comma ( !')' . )* )? ')' )

RSigDescTableDecl       <- T( 'DescriptorTable' _ '(' RSigDescTableEntity ( Comma RSigDescTableEntity )*  ')' )
RSigDescTableEntity     <- _ RSigCBVDecl _ / _ RSigSRVDecl _ / _ RSigUAVDecl _ / _ RSigDescTableSampler _ / _ RSigDeclOptions _
RSigDescTableSampler    <- T( 'Sampler' _ '(' RegisterId ( Comma RSDescNumDecl )? ')' )

RSigDeclOptions     <- _ RSigVisibilityDecl _ / _ RSDescNumDecl _
RSigVisibilityDecl  <- T( 'visibility' _ '=' RSigVisibilityFlags )
RSigVisibilityFlags	<- _ 'SHADER_VISIBILITY_ALL' _ / _ 'SHADER_VISIBILITY_VERTEX' _ / _ 'SHADER_VISIBILITY_HULL' _ / _ 'SHADER_VISIBILITY_DOMAIN' _ / _ 'SHADER_VISIBILITY_GEOMETRY' _ / _ 'SHADER_VISIBILITY_PIXEL' _
RSDescNumDecl       <- T( 'numDescriptors' _ '=' Int )

# --- PrePostPass ---
PrePostPass             <-  T( (PrePass/ PostPass){0, 2} )
PrePass                 <-  T( 'PrePass' _ '{' FixedFunction* '}' )
PostPass                <-  T( 'PostPass' _ '{' FixedFunction* '}' )
FixedFunction           <-  T( FixedFunctionClearColor / FixedFunctionClearDepth )
FixedFunctionClearColor	<-	T( 'ClearColor' _ '(' Float4 ')' )
FixedFunctionClearDepth <-  T( 'ClearDepth' _ '(' Float ')' )

# --- ShaderDefs ---
Function 			<- T( < Ident Ident '(' ( !')' . )* ')' _ '{' ( !'}' . )* '}' > )
VertAttr            <- T(  'struct' < Ident '{' VertAttrContent '}' > )
Resource            <- T(  ResourceAttr ( ConstBuff / Texture / Sampler / RWTexture ) )
ConstBuff           <- T( 'cbuffer' < Ident ':' RegisterDecl  '{' ConstBuffContent '}' > )
Texture             <- T( 'Texture2D' < Ident ':' RegisterDecl > )
RWTexture           <- T( 'RWTexture2D' _ '<' DataType '>' Ident ':' RegisterDecl ) 
Sampler             <- T( 'SamplerState' < Ident ':' RegisterDecl > )
ResourceAttr        <- T( '[' < ResourceScope Comma ResourceUpdate ( Comma ResourceBind)?> ']' )
ResourceScope       <- _ 'Local' _ / _ 'Global' _
ResourceUpdate      <- _ 'PerObject' _ / _ 'PerPass' _
ResourceBind        <- T( 'Bind' _ ':' Ident )

ConstBuffContent	<- T( ConstBuffField* )
ConstBuffField		<- T( DataType Ident ';' )

VertAttrContent     <- T( VertAttrField* ) 
VertAttrField       <- T( ~VertAttrFieldAttr? DataType Ident ':' ResourceFieldSemantic ';' )
VertAttrSlots       <- T( '[' VertAttrFieldSlot? ( Comma VertAttrFieldSlot )* ']' )
VertAttrFieldSlot   <- T( Ident ':' Int )
VertAttrFieldAttr   <- _ 'nointerpolation' _ / _ 'centroid' _

DataType       <- _ 'float4x4' _ / _ 'float4' _ / _ 'float2' _ / _ 'int' _
ResourceFieldSemantic   <- T( Word Int? )

# --- Tokens ---
Ident 	        <- T( < [a-zA-Z_][a-zA-Z0-9_]* > )
RegisterDecl    <- T( 'register' _ '(' RegisterId ')' )
RegisterId      <- T( [btsu] Int )

# --- In code macro ---
# I don't actually need this. According to the current plant, there will
# be no macro definitions after preprocessing, so parser will never see this
# MacroList    <- T( < (MacroInclude)*  > )
# MacroInclude <- T( '#include' __ < FileName > EndOfLine )
# FileName     <- T( < [a-zA-Z][a-zA-Z0-9]* '.' [a-zA-Z][a-zA-Z0-9]* > )


# --- Types ---
Bool    <- _ 'true' _ / _ 'false' _
Float   <- T( < [0-9][0-9]* '.' [0-9][0-9]* > )
Int     <- T( < [0-9][0-9]* > )
Word    <- T( < [a-zA-Z_][a-zA-Z_]* > )
Float4	<- T( Float Comma Float Comma Float Comma Float )

# --- Punctuation ---
~Comma <-  T( < ',' > )

# --- Whitespaces ---
~_   <- Spacing*

Spacing   <- (Space / Comment)
Comment   <- '//' ( !EndOfLine . )* EndOfLine
Space     <- ' ' / '\t' / EndOfLine
EndOfLine <- '\r\n' / '\n' / '\r'

# --- Parser macro ---
T(x)        <- (_ x _)