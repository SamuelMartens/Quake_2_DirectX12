Pass <- PassInput State Resources ShaderList

Resources   <- T( < (Resource / VertAttr)* > )
State       <- T( < ( ColorTargetSt / DepthTargetSt / ViewportSt / BlendEnabledSt / TopologySt / DepthWriteMaskSt / SrcBlendAlphaSt / SrcDestAlphaSt )* > )
ShaderList  <- T( < Shader* > )

# --- Pass properties
PassInput       <- T( 'PassInput' _ ':' < PassInputIdent (Comma PassInputIdent)* > )
PassInputIdent  <- _ 'Static' _ / _ 'Dynamic' _ / _ 'Particles' _ / _ 'UI' _ / _ 'PostProcess' _

# --- Shader code ---
ShaderExternalDecl  <- T( 'External'_ ':' < Ident? ( Comma Ident )* > )
ShaderSource        <- T( '<[' < ( !"]>"  . )* > ']>' ) 
Shader              <- T( < ShaderTypeDecl ShaderExternalDecl ShaderSource > )
ShaderTypeDecl      <- T( 'Type' _ ':' _ < ShaderType > ) 
ShaderType			<- _'VS' _ / _ 'GS' _ / _ 'PS' _

# --- State ---
ColorTargetSt   	<- T( 'ColorTarget' _ ':' < Ident > )
DepthTargetSt   	<- T( 'DepthTarget' _ ':' < Ident > )
ViewportSt      	<- T( 'Viewport' _ ':' < (Float / Int) (Comma (Float / Int)){3} > )
BlendEnabledSt  	<- T( 'BlendEnabled' _ ':' < Bool > )
SrcBlendAlphaSt		<- T( 'SrcBlendAlpha' _ ':' < BlendStValues > )
SrcDestAlphaSt		<- T( 'DestBlendAlpha' _ ':' < BlendStValues > )
TopologySt			<- T( 'Topology' _ ':' < TopologyStValues > )
DepthWriteMaskSt	<- T( 'DepthWriteMask' _ ':' < Bool > )

BlendStValues		<- _ 'SrcAlpha' _ / _ 'InvSrcAlpha' _
TopologyStValues    <- _ 'Triangle' _ / _ 'Point' _

# --- Resources ---
VertAttr            <- T( 'struct' < Ident '{' ResourceContent '}' > )
Resource            <- T(  ResourceAttr ( ConstBuff / Texture / Sampler) )
ConstBuff           <- T( 'cbuffer' < Ident ':' RegisterIdent  '{' ResourceContent '}' > )
Texture             <- T( 'Texture2D' < Ident ':' RegisterIdent > )
Sampler             <- T( 'SamplerState' < Ident ':' RegisterIdent > )
ResourceAttr        <- T( '[' < ResourceScope ',' ResourceUpdate > ']' )
ResourceScope       <- _ 'Global' _ / _ 'Local' _
ResourceUpdate      <- _ 'PerObject' _ / _ 'PerPass' _ / _ 'PerFrame' _ / _ 'OnInit' _


# --- Tokens ---
Ident 	        <- T( < [a-zA-Z_][a-zA-Z0-9_]* > )
RegisterIdent   <- T( 'register' _ '(' _ < [bts] [0-9]+ > _ ')' )
ResourceContent <- [^}]*

# --- In code macro ---
# I don't actually need this. According to the current plant, there will
# be no macro definitions after preprocessing, so parser will never see this
# MacroList    <- T( < (MacroInclude)*  > )
# MacroInclude <- T( '#include' __ < FileName > EndOfLine )
# FileName     <- T( < [a-zA-Z][a-zA-Z0-9]* '.' [a-zA-Z][a-zA-Z0-9]* > )


# --- Types ---
Bool   <- _ 'true' _ / _ 'false' _
Float  <- T( < [0-9][0-9]* '.' [0-9][0-9]* > )
Int    <- T( < [0-9][0-9]* > )

# --- Punctuation ---
Comma <- _ ',' _

# --- Whitespaces ---
# Carefull with this this. Because of greedy nature of 'star symbol, two these symbols
# in a row will never work.
# TODO investigate whitespace ignore option like %whitespace
~_   <- Spacing*

Spacing   <- (Space / Comment)
Comment   <- '//' ( !EndOfLine . )* EndOfLine
Space     <- ' ' / '\t' / EndOfLine
EndOfLine <- '\r\n' / '\n' / '\r'

# --- Parser macro ---
T(x)        <- (_ x _)